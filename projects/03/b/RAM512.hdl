// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(
            in = load,
            sel = address[0..2],
            a = l0,
            b = l1,
            c = l2,
            d = l3,
            e = l4,
            f = l5,
            g = l6,
            h = l7
            );

    RAM64(in = in, load = l0, address = address[3..8], out = t0);
    RAM64(in = in, load = l1, address = address[3..8], out = t1);
    RAM64(in = in, load = l2, address = address[3..8], out = t2);
    RAM64(in = in, load = l3, address = address[3..8], out = t3);
    RAM64(in = in, load = l4, address = address[3..8], out = t4);
    RAM64(in = in, load = l5, address = address[3..8], out = t5);
    RAM64(in = in, load = l6, address = address[3..8], out = t6);
    RAM64(in = in, load = l7, address = address[3..8], out = t7);

    Mux8Way16(
            a = t0,
            b = t1,
            c = t2,
            d = t3,
            e = t4,
            f = t5,
            g = t6,
            h = t7,
            sel = address[0..2],
            out = out
            );
}
